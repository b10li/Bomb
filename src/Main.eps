import player.pMain;
import player.Bomb;
import player.Unit;
import map.Mapper;
import map.Render;
import EUDEdit;

var GameState;
const PlayerLife = EUDArray(6);

function RoundStart();
function RoundEnd();
function DisplayTextToAllPlayers(number);
function GameEnd(player);
function DatEdit();
function PlayerDeath(player);

function onPluginStart()
{
	EUDPlayerLoop()();
	DisplayText("\r\n\r\n\r\n\r\n\r\n\x13\x1b폭탄: [\x07 A \x1b] 키\r\n\r\n\r\n\r\n\r\n");
	EUDEndPlayerLoop();
	randomize();
	LeaderBoardComputerPlayers(Disable);
	LeaderBoardScore(Custom, "/2");
	EUDEdit.DatEdit();
}

function beforeTriggerExec() 
{
	if(GameState == 0)
	{
		RoundStart();
	}

	if(GameState == 1)
	{
		Bomb.CheckBomb();
		EUDPlayerLoop()();
		var player = getcurpl();
		if(player < 6)
		{
			pMain.PlaceBomb(Unit.getPlayerEpd(player));
			pMain.FollowLocation(player);
			pMain.ItemCollect(player);
			pMain.OffRide(player);
			PlayerDeath(player);
		}
		EUDEndPlayerLoop();
		RoundEnd();
	}

}

function afterTriggerExec() 
{
	// eudturbo
	SetMemory(0x6509A0, SetTo, 0);  
}

function RoundStart()
{
	Render.ClearMap();
	Mapper.getMap();
	Render.DrawMap();
	
	EUDPlayerLoop()();
	var player = getcurpl();
	if(player < 6) //P1~P6
		pMain.InitPlayer(player);
	PlayerLife[player] = 1;
	EUDEndPlayerLoop();
	GameState = 1; // <------------------
}

function PlayerDeath(player)
{
	if(Command(player, Exactly, 0, '(men)'))
	{
		if(Deaths(player, Exactly, 0, 3) && Deaths(player, Exactly, 0, 5))
		{
			PlayerLife[player] = 0;
		}
	}
}

function RoundEnd()
{
	EUDPlayerLoop()();
	// check player solo 
	if(getcurpl() < 6) {
		if(PlayerLife[getcurpl()]) {
			SetDeaths(7, Add, 1, 199);
		}
	}
	EUDEndPlayerLoop();

	if(Deaths(7, Exactly, 1, 199) && Accumulate($Force1, AtLeast, 1, OreAndGas))
	{
		if(Command($Force1, AtMost, 1, '(men)'))
		{
			EUDPlayerLoop()();
			const player = getcurpl();
			if(Command(player, Exactly, 1, '(men)'))
			{
				SetScore(player, Add, 1, Custom);	// win score
				DisplayTextToAllPlayers(player);
			}
			GameEnd(player);
			EUDEndPlayerLoop();
			GameState = 0; // <------------------
			LeaderBoardScore(Custom, "/2");
		}
	}
	// reset count
	SetDeaths(7, SetTo, 0, 199);
}

function GameEnd(player)
{
	for(var i=0; i<6; i++)
	{
		if (Score(i, Custom, Exactly, 2))
		{
			DisplayTextToAllPlayers(i);
			Victory();
		}
	}
}

function DisplayTextToAllPlayers(number)
{
	EUDPlayerLoop()();
	if(number == 0) DisplayText("\r\n\r\n\r\n\r\n\r\n\x13빨강이 이겼다\r\n\r\n\r\n\r\n\r\n");
	else if(number == 1) DisplayText("\r\n\r\n\r\n\r\n\r\n\x13파랑이 이겼다\r\n\r\n\r\n\r\n\r\n");
	else if(number == 2) DisplayText("\r\n\r\n\r\n\r\n\r\n\x13연두가 이겼다\r\n\r\n\r\n\r\n\r\n");
	else if(number == 3) DisplayText("\r\n\r\n\r\n\r\n\r\n\x13보라가 이겼다\r\n\r\n\r\n\r\n\r\n");
	else if(number == 4) DisplayText("\r\n\r\n\r\n\r\n\r\n\x13주황이 이겼다\r\n\r\n\r\n\r\n\r\n");
	else if(number == 5) DisplayText("\r\n\r\n\r\n\r\n\r\n\x13똥색이 이겼다\r\n\r\n\r\n\r\n\r\n");
	EUDEndPlayerLoop();
}

