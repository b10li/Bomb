import player.pMain;
import player.Bomb;
import player.Unit;
import map.Mapper;
import map.Render;
import EUDEdit;

var GameState;
const PlayerLife = EUDArray(6);

function RoundStart();
function RoundEnd();
function WinnerText(number);
function GameEnd(player);
function DatEdit();
function PlayerDeath(player);
function ReadyCount();

function onPluginStart()
{
	randomize();
	LeaderBoardComputerPlayers(Disable);
	LeaderBoardScore(Custom, "/2");
	EUDEdit.DatEdit();
	EUDPlayerLoop()();
	DisplayText("\r\n\r\n\r\n\r\n\r\n\x13\x1b폭탄: [\x07 A \x1b] 키\r\n\r\n\r\n\r\n\r\n");
	EUDEndPlayerLoop();
    bwrite(0x581D76 + 8 * $P8, 40);
}

function beforeTriggerExec() 
{
	if(GameState == 0)
	{
		RoundStart();
	}

	if(GameState == 1)
	{
		RemoveUnit('(men)', $P12);
		Bomb.CheckBomb();
		EUDPlayerLoop()();
		var player = getcurpl();
		if(player < 6)
		{
			PlayerDeath(player);
			pMain.PlaceBomb(Unit.PlayerEpd[player]);
			pMain.FollowLocation(player);
			pMain.ItemCollect(player);
			pMain.OffRide(player);
		}
		EUDEndPlayerLoop();
		RoundEnd();
	}

	if(GameState == 3)
	{// before count
		SetDeaths(7, SetTo, 24*4, 226);
		GameState = 2;
	}

	if(GameState == 2)
	{// countdown
		ReadyCount();
	}
}

function afterTriggerExec() 
{
	// eudturbo
	SetMemory(0x6509A0, SetTo, 0);  
}

function ReadyCount()
{
	SetDeaths(7, Subtract, 1, 226);
	if(Deaths(7, Exactly, 24*3, 226))
	{
		EUDPlayerLoop()();
		PlayWAV("sound\\glue\\countdown.wav");
		EUDEndPlayerLoop();
	}
	if(Deaths(7, Exactly, 24*2, 226))
	{
		EUDPlayerLoop()();
		PlayWAV("sound\\glue\\countdown.wav");
		EUDEndPlayerLoop();
	}
	if(Deaths(7, Exactly, 24*1, 226))
	{
		EUDPlayerLoop()();
		PlayWAV("sound\\glue\\countdown.wav");
		EUDEndPlayerLoop();
	}
	if(Deaths(7, Exactly, 0, 226))
	{
		EUDPlayerLoop()();
		PlayWAV("sound\\Protoss\\ZEALOT\\PZeRdy00.wav");
		DisplayText("\r\n\r\n\r\n\r\n\r\n\x13\x1b게임 시작!\r\n\r\n\r\n\r\n\r\n");
		EUDEndPlayerLoop();
		GameState = 1;
	}
}

function RoundStart()
{
	Render.ClearMap();
	Mapper.getMap();
	Render.DrawMap();
	
	EUDPlayerLoop()();
	var player = getcurpl();
	if(player < 6) //P1~P6
		pMain.InitPlayer(player);
	PlayerLife[player] = 1;
	EUDEndPlayerLoop();
	GameState = 3; // <------------------
}

function PlayerDeath(player)
{
	if(Command(player, Exactly, 0, 15) && Command(player, Exactly, 0, 3) && Command(player, Exactly, 0, 5))
	{
		if(Deaths(player, Exactly, 0, 3) && Deaths(player, Exactly, 0, 5))
		{
			PlayerLife[player] = 0;
		}
	}
}

function RoundEnd()
{
	for(var player=0; player<6; player++)
	{// check player solo 
		if(!playerexist(player)) continue;
		if(PlayerLife[player]) SetDeaths(7, Add, 1, 199);
	}

	if(Deaths(7, Exactly, 1, 199) && Accumulate($Force1, AtLeast, 1, OreAndGas))
	{// solo survive
		for(var player=0; player<6; player++)
		{
			if(!playerexist(player)) continue;
			if(PlayerLife[player])
			{// win score
				SetScore(player, Add, 1, Custom);	
				WinnerText(player);
				break;
			}
		}
		GameState = 0; // <------------------
		LeaderBoardScore(Custom, "/2");
		GameEnd();

	}
	// reset count
	SetDeaths(7, SetTo, 0, 199);
}

function GameEnd()
{
	for(var i=0; i<6; i++)
	{
		if (Score(i, Custom, AtLeast, 2))
		{
			EUDPlayerLoop()();
			if(getcurpl() < 6)
				Victory();
			EUDEndPlayerLoop();
		}
	}
}

function WinnerText(number)
{
	EUDPlayerLoop()();
	if(number == 0) DisplayText("\r\n\r\n\r\n\r\n\r\n\x13빨강이 이겼다\r\n\r\n\r\n\r\n\r\n");
	else if(number == 1) DisplayText("\r\n\r\n\r\n\r\n\r\n\x13파랑이 이겼다\r\n\r\n\r\n\r\n\r\n");
	else if(number == 2) DisplayText("\r\n\r\n\r\n\r\n\r\n\x13연두가 이겼다\r\n\r\n\r\n\r\n\r\n");
	else if(number == 3) DisplayText("\r\n\r\n\r\n\r\n\r\n\x13보라가 이겼다\r\n\r\n\r\n\r\n\r\n");
	else if(number == 4) DisplayText("\r\n\r\n\r\n\r\n\r\n\x13주황이 이겼다\r\n\r\n\r\n\r\n\r\n");
	else if(number == 5) DisplayText("\r\n\r\n\r\n\r\n\r\n\x13똥색이 이겼다\r\n\r\n\r\n\r\n\r\n");
	EUDEndPlayerLoop();
}

